apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- JAVADOC ---

javadoc {
	failOnError = true
}

// --- JACOCO ---

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/moleculer/logger/**')
        })
    }    
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// --- DEPENDENCIES ---

dependencies {

    // ============= COMPILATION =============

    testImplementation 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'

	// =============== TESTING ===============

	// https://mvnrepository.com/artifact/com.openpojo/openpojo
	testImplementation group: 'com.openpojo', name: 'openpojo', version: '0.8.10'	
		
	// =============== LOGGING ===============
	
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-api
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'
       
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-jdk14
	implementation group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.30'

	// https://mvnrepository.com/artifact/org.slf4j/log4j-over-slf4j
	implementation group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.30'

	// https://mvnrepository.com/artifact/org.slf4j/jcl-over-slf4j
	implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.30'

	// https://mvnrepository.com/artifact/com.diogonunes/JCDP
	implementation group: 'com.diogonunes', name: 'JCDP', version: '2.0.3.1'

	// =============== SERIALIZATION ===============

    // --- DATATREE API ---
    	    
	// https://mvnrepository.com/artifact/com.github.berkesa/datatree-adapters
	implementation group: 'com.github.berkesa', name: 'datatree-adapters', version: '1.0.14'

    // --- PROMISE API ---
    
	// https://mvnrepository.com/artifact/com.github.berkesa/datatree-promise
	// implementation project(':datatree-promise')
	implementation group: 'com.github.berkesa', name: 'datatree-promise', version: '1.0.7'

    // --- SERIALIZER APIS ---
   
	// https://mvnrepository.com/artifact/de.undercouch/bson4jackson
	implementation group: 'de.undercouch', name: 'bson4jackson', version: '2.11.0'
 
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-cbor
	implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor', version: '2.11.3'
 
	// https://mvnrepository.com/artifact/software.amazon.ion/ion-java
	implementation group: 'software.amazon.ion', name: 'ion-java', version: '1.5.1'
 
	// https://mvnrepository.com/artifact/org.msgpack/msgpack
	implementation group: 'org.msgpack', name: 'msgpack', version: '0.6.12'
 
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-smile
	implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-smile', version: '2.11.3'
 
	// =============== MONITORING ===============
	
	// --- SIGAR API TO QUERY THE CPU USAGE ---
	
	// https://mvnrepository.com/artifact/org.fusesource/sigar
	implementation (group: 'org.fusesource', name: 'sigar', version: '1.6.4') {
		exclude group: 'log4j', module: 'log4j'
	}
	
	// =============== CDI FRAMEWORKS ===============
	
    // --- SPRING FRAMEWORK (REQUIRED FOR MOLECULER RUNNER) ---

	// https://mvnrepository.com/artifact/org.springframework/spring-context
	implementation group: 'org.springframework', name: 'spring-context', version: '5.2.9.RELEASE'

    // --- SPRING BOOT (OPTIONAL!) ---
    
	// https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter
	implementation (group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.3.4.RELEASE') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	}

	// =============== CACHES ===============
	
	// --- OHC OFF-HEAP CACHE ---
	
	// https://mvnrepository.com/artifact/org.caffinitas.ohc/ohc-core-j8
	implementation group: 'org.caffinitas.ohc', name: 'ohc-core-j8', version: '0.6.1'

	// --- JCACHE API + IMPLEMENTATION ---
		
	// https://mvnrepository.com/artifact/org.jsr107.ri/cache-ri-impl
	implementation group: 'org.jsr107.ri', name: 'cache-ri-impl', version: '1.1.1'
		
	// https://mvnrepository.com/artifact/javax.cache/cache-api
	implementation group: 'javax.cache', name: 'cache-api', version: '1.1.1'

	// =============== TRANSPORTERS ===============
	
	// --- DEPENDENCY OF THE REDIS TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/biz.paluch.redis/lettuce
	implementation group: 'biz.paluch.redis', name: 'lettuce', version: '4.5.0.Final'

	// --- DEPENDENCY OF THE NATS TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/io.nats/jnats
	implementation group: 'io.nats', name: 'jnats', version: '2.8.0'

	// --- DEPENDENCY OF THE NATS-STREAMING TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/io.nats/java-nats-streaming
	implementation group: 'io.nats', name: 'java-nats-streaming', version: '2.2.3'

	// --- DEPENDENCY OF THE MQTT TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/org.eclipse.paho/org.eclipse.paho.client.mqttv3
	compile group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version: '1.2.5'

	// --- DEPENDENCY OF THE JMS TRANSPORTER ---

	// https://mvnrepository.com/artifact/javax.jms/javax.jms-api
	implementation group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'

	// --- ACTIVE QM CLIENT API FOR JMS TESTING ---

	// https://mvnrepository.com/artifact/org.apache.activemq/activemq-client
	implementation group: 'org.apache.activemq', name: 'activemq-client', version: '5.16.0'

	// --- DEPENDENCY OF THE AMQP/RABBITMQ TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/com.rabbitmq/amqp-client
	implementation group: 'com.rabbitmq', name: 'amqp-client', version: '5.9.0'

	// --- DEPENDENCY OF THE KAFKA TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/org.apache.kafka/kafka-clients
	implementation group: 'org.apache.kafka', name: 'kafka-clients', version: '2.6.0'

	// =============== METRICS ===============

	// --- DEPENDENCY OF THE DROPWIZARD METRIC REGISTRY ---
	
	// https://mvnrepository.com/artifact/com.codahale.metrics/metrics-core
	compile group: 'com.codahale.metrics', name: 'metrics-core', version: '3.0.2'

	// --- DEPENDENCY OF THE MICROMETER METRIC REGISTRY ---
	
	// https://mvnrepository.com/artifact/io.micrometer/micrometer-core
	compile group: 'io.micrometer', name: 'micrometer-core', version: '1.5.5'

	// --- TEST MICROMETER METRIC REGISTRIES ---
	
	// https://mvnrepository.com/artifact/io.micrometer/micrometer-registry-new-relic
	compile group: 'io.micrometer', name: 'micrometer-registry-new-relic', version: '1.5.5'

	// https://mvnrepository.com/artifact/io.micrometer/micrometer-registry-jmx
	compile group: 'io.micrometer', name: 'micrometer-registry-jmx', version: '1.5.5'

	// https://mvnrepository.com/artifact/io.micrometer/micrometer-registry-datadog
	compile group: 'io.micrometer', name: 'micrometer-registry-datadog', version: '1.5.5'

}


sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

// version = '1.2.14-SNAPSHOT'
version = '1.2.14'

modifyPom {
    project {
        artifactId 'moleculer-java'
        name 'Moleculer Microservices Framework'
        description 'Fast & powerful microservices framework for Java and Node.js.'
        url 'https://moleculer-java.github.io/moleculer-java/'
        inceptionYear '2018'

        scm {
            url 'https://moleculer-java.github.io/moleculer-java/'
            connection 'scm:https://github.com/moleculer-java/moleculer-java.git'
            developerConnection 'scm:git://github.com/moleculer-java/moleculer-java.git'
        }

        licenses {
            license {
                name "The MIT License"
                url "http://www.opensource.org/licenses/MIT"
                distribution 'repo'
            }
        }
        
        dependencies {
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'datatree-adapters'
                version '1.0.14'
                scope 'runtime'
            }
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'datatree-promise'
                version '1.0.7'
                scope 'runtime'
            }
            dependency {
                groupId 'io.micrometer'
                artifactId 'micrometer-core'
                version '1.5.5'
                scope 'runtime'
            }
            dependency {
                groupId 'org.slf4j'
                artifactId 'slf4j-api'
                version '1.7.30'
                scope 'runtime'
            }            
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}

// --- JAR ---

jar {
    baseName = 'moleculer'
    version =  '1.2.14'
}

// --- TESTING ---

test {
	systemProperty 'java.util.logging.SimpleFormatter.format', '%4$-7s %5$s %n'
    
    exclude '**/KafkaTransporterTest.class'
    exclude '**/JmsTransporterTest.class'
	// exclude '**/FileSystemTransporterTest.class'
    exclude '**/NatsStreamingTransporterTest.class'

    exclude '**/KafkaStreamTest.class'
    exclude '**/JmsStreamTest.class'
    exclude '**/NatsStreamingStreamTest.class'
    
    exclude '**/TransporterTestSuite.class'
}