apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

// --- JAVADOC ---

javadoc {
	failOnError = false
}

// --- JACOCO ---

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}

// --- CONFIGURATIONS ---

configurations {
    runtime
    ecj
}

configurations.all {
    // resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

// --- REPOSITORIES ---

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}

// --- DEPENDENCIES ---

dependencies {

    testCompile 'junit:junit:4.12'
	ecj 'org.eclipse.jdt.core.compiler:ecj:4.4.2'
				
	// =============== TESTING ===============


	// https://mvnrepository.com/artifact/com.openpojo/openpojo
	testCompile group: 'com.openpojo', name: 'openpojo', version: '0.8.10'	

		
	// =============== LOGGING ===============
	
	
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-api
	compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
       
	// http://mvnrepository.com/artifact/org.slf4j/slf4j-jdk14
	compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.25'

	// https://mvnrepository.com/artifact/org.slf4j/log4j-over-slf4j
	compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.25'

	// https://mvnrepository.com/artifact/org.slf4j/jcl-over-slf4j
	compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: '1.7.25'

	// https://mvnrepository.com/artifact/com.diogonunes/JCDP
	compile group: 'com.diogonunes', name: 'JCDP', version: '2.0.3.1'


	// =============== SERIALIZATION ===============


    // --- DATATREE API ---
    	    
	// https://mvnrepository.com/artifact/com.github.berkesa/datatree-adapters
	compile group: 'com.github.berkesa', name: 'datatree-adapters', version: '1.0.5'

    // --- PROMISE API ---
    
	// https://mvnrepository.com/artifact/com.github.berkesa/datatree-promise   
	compile group: 'com.github.berkesa', name: 'datatree-promise', version: '1.0.2'

    // --- SERIALIZER APIS ---
   
	// https://mvnrepository.com/artifact/de.undercouch/bson4jackson
	compile group: 'de.undercouch', name: 'bson4jackson', version: '2.9.0'
 
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-cbor
	compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor', version: '2.9.0.pr3'
 
	// https://mvnrepository.com/artifact/software.amazon.ion/ion-java
	compile group: 'software.amazon.ion', name: 'ion-java', version: '1.0.3'
 
	// https://mvnrepository.com/artifact/org.msgpack/msgpack
	compile group: 'org.msgpack', name: 'msgpack', version: '0.6.12'
 
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-smile
	compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-smile', version: '2.9.0.pr3'
 
 
	// =============== MONITORING ===============
	
	
	// --- SIGAR API TO QUERY THE CPU USAGE ---
	
	// https://mvnrepository.com/artifact/org.fusesource/sigar
	compile (group: 'org.fusesource', name: 'sigar', version: '1.6.4') {
		exclude group: 'log4j', module: 'log4j'
	}
	
	
	// =============== CDI FRAMEWORKS ===============
	 
	
    // --- SPRING DEPENDENCY INJECTION FRAMEWORK ---

	// https://mvnrepository.com/artifact/org.springframework/spring-core
	compile group: 'org.springframework', name: 'spring-context', version: '5.0.0.RELEASE'

		
	// =============== CACHES ===============
	
	
	// --- OHC OFF-HEAP CACHE ---
	
	// https://mvnrepository.com/artifact/org.caffinitas.ohc/ohc-core-j8
	compile group: 'org.caffinitas.ohc', name: 'ohc-core-j8', version: '0.6.1'

	// --- JCACHE API + IMPLEMENTATION ---
		
	// https://mvnrepository.com/artifact/org.jsr107.ri/cache-ri-impl
	compile group: 'org.jsr107.ri', name: 'cache-ri-impl', version: '1.1.0'
		
	// https://mvnrepository.com/artifact/javax.cache/cache-api
	//compile group: 'javax.cache', name: 'cache-api', version: '1.0.0'


	// =============== TRANSPORTERS ===============
	
	
	// --- DEPENDENCY OF THE REDIS TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/biz.paluch.redis/lettuce
	compile group: 'biz.paluch.redis', name: 'lettuce', version: '4.4.1.Final'

	// --- DEPENDENCY OF THE NATS TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/io.nats/jnats
	compile group: 'io.nats', name: 'jnats', version: '1.0'

	// --- DEPENDENCY OF THE MQTT TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/net.sf.xenqtt/xenqtt
	compile group: 'net.sf.xenqtt', name: 'xenqtt', version: '0.9.7'

	// --- DEPENDENCY OF THE JMS TRANSPORTER ---

	// https://mvnrepository.com/artifact/javax.jms/javax.jms-api
	compile group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'

	// --- ACTIVE QM CLIENT API FOR JMS TESTING ---

	// https://mvnrepository.com/artifact/org.apache.activemq/activemq-client
	compile group: 'org.apache.activemq', name: 'activemq-client', version: '5.15.3'

	// --- DEPENDENCY OF THE AMQP/RABBITMQ TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/com.rabbitmq/amqp-client
	compile group: 'com.rabbitmq', name: 'amqp-client', version: '5.0.0'

	// --- DEPENDENCY OF THE GOOGLE CLOUD PUB/SUB TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/com.google.cloud/google-cloud-pubsub
	compile group: 'com.google.cloud', name: 'google-cloud-pubsub', version: '0.30.0-beta'

	// --- DEPENDENCY OF THE KAFKA TRANSPORTER ---
	
	// https://mvnrepository.com/artifact/org.apache.kafka/kafka-clients
	compile group: 'org.apache.kafka', name: 'kafka-clients', version: '1.0.0'

}


sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.github.berkesa'

version = '1.0.2'

modifyPom {
    project {
        artifactId 'moleculer-java'
        name 'Moleculer Microservices Framework'
        description 'Fast & powerful microservices framework for Java.'
        url 'https://moleculer-java.github.io/moleculer-java/'
        inceptionYear '2018'

        scm {
            url 'https://moleculer-java.github.io/moleculer-java/'
            connection 'scm:https://github.com/moleculer-java/moleculer-java.git'
            developerConnection 'scm:git://github.com/moleculer-java/moleculer-java.git'
        }

        licenses {
            license {
                name "The MIT License"
                url "http://www.opensource.org/licenses/MIT"
                distribution 'repo'
            }
        }
        
        dependencies {
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'datatree-adapters'
                version '1.0.5'
                scope 'runtime'
            }
            dependency {
                groupId 'com.github.berkesa'
                artifactId 'datatree-promise'
                version '1.0.2'
                scope 'runtime'
            }            
            dependency {
                groupId 'org.slf4j'
                artifactId 'slf4j-api'
                version '1.7.25'
                scope 'runtime'
            }            
        }

        developers {
            developer {
                id 'berkesa'
                name 'Andras Berkes'
                email 'andras.berkes@programmer.net'
            }
        }
    }
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}

// --- COMPILATION ---

compileJava {
	options.fork = true
	options.forkOptions.with {
		executable = 'java'
		jvmArgs = ['-classpath', project.configurations.ecj.asPath, 'org.eclipse.jdt.internal.compiler.batch.Main', '-nowarn']
	}
}

// --- JAR ---

jar {
    baseName = 'moleculer'
    version =  '1.0.2'
}

// --- TESTING ---

test {
	systemProperty 'java.util.logging.SimpleFormatter.format', '%4$-7s %5$s %n'

    exclude '**/KafkaTransporterTest.class'
    exclude '**/JmsTransporterTest.class'
    exclude '**/GoogleTransporterTest.class'
}

// task copyRuntimeLibs(type: Copy) {
// into "lib"
// from configurations.runtime
// }
